buildscript {
    def taskRequests = getGradle().getStartParameter().getTaskRequests().toString().toLowerCase()
    def isPlay = !taskRequests.contains("foss")

    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "25.1.8937393"
        kotlinVersion = "1.8.0"
        kotlin_version = kotlinVersion
        glideVersion = "4.11.0"
        supportLibVersion = "28.0.0"
        libre_build = !(isPlay.toBoolean())
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        if (isPlay) {
            classpath 'com.google.gms:google-services:4.4.1'
            classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.0'
            classpath "com.bugsnag:bugsnag-android-gradle-plugin:8.+"
        }
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

allprojects {
    repositories {
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }

        maven {
            // expo-camera bundles a custom com.google.android:cameraview
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }
    }
}

subprojects { subproject ->	
    afterEvaluate {	
        if (!project.name.equalsIgnoreCase("app") && project.hasProperty("android")) {	
            android {	
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
                defaultConfig {	
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }	
            }	
        }	
    }
}

apply plugin: "com.facebook.react.rootproject"
